{
    parserClass="org.intellij.sdk.language.parser.McFunctionParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="McFunction"
    psiImplClassSuffix="Impl"
    psiPackage="org.intellij.sdk.language.psi"
    psiImplPackage="org.intellij.sdk.language.psi.impl"

    elementTypeHolderClass="org.jonatjano.mclang.psi.McFunctionTypes"
    elementTypeClass="org.jonatjano.mclang.psi.McFunctionElementType"
    tokenTypeClass="org.jonatjano.mclang.psi.McFunctionTokenType"
}
//simpleFile ::= item_*
//
//private item_ ::= (property|COMMENT|CRLF)
//
//property ::= (KEY? SEPARATOR VALUE?) | KEY

mcFunctionFile ::= item_*

private item_ ::= (command|COMMENT|CRLF)

vec3 ::= (float float float)|(TOK_TILDE float? TOK_TILDE float? TOK_TILDE float?)|(TOK_CIRCONFLEX float? TOK_CIRCONFLEX float? TOK_CIRCONFLEX float?)
u_integer ::= INTEGER_ONE|INTEGER_NOT_ONE
integer ::= u_integer|INTEGER_SIGNED
u_float ::= U_FLOAT_RULE|u_integer
float ::= integer|u_float|FLOAT_SIGNED
integer_range ::= (integer TOK_DOT TOK_DOT integer)|(integer TOK_DOT TOK_DOT)|(TOK_DOT TOK_DOT integer)|integer
u_float_range ::= (u_float TOK_DOT TOK_DOT u_float)|(u_float TOK_DOT TOK_DOT)|(TOK_DOT TOK_DOT u_float)|u_float
float_range ::= (float TOK_DOT TOK_DOT float)|(float TOK_DOT TOK_DOT)|(TOK_DOT TOK_DOT float)|float

mob_effect ::= word|integer
item_slot ::= (word TOK_DOT word)|(word TOK_DOT integer)|word|integer
item_stack ::= (word TOK_COLON)?word nbt_object
fractional ::= float (TOK_FRACTIONAL float)?
resource_location ::= (word TOK_COLON)? word (TOK_SLASH word)*
resource_location_tag ::= TOK_HASH? resource_location
word ::= PLAYER_PSEUDO|WORD_RULE|GAMEMODE_KEYWORDS|ENTITY_ANCHOR
string_single_quote_child ::= TOK_SINGLE_QUOTE (ANY_NO_QUOTE|TOK_CANCELLED_SINGLE_QUOTE|TOK_CANCELLED_DUAL_QUOTE|TOK_CANCELLED_BACKSLASH)* TOK_SINGLE_QUOTE
string_dual_quote_child ::= TOK_DUAL_QUOTE (ANY_NO_QUOTE|TOK_CANCELLED_SINGLE_QUOTE|TOK_CANCELLED_DUAL_QUOTE|TOK_CANCELLED_BACKSLASH)* TOK_DUAL_QUOTE
string_single_quote_parent ::= TOK_SINGLE_QUOTE (ANY_NO_QUOTE|TOK_CANCELLED_SINGLE_QUOTE|TOK_CANCELLED_BACKSLASH|string_dual_quote_child)* TOK_SINGLE_QUOTE
string_dual_quote_parent ::= TOK_DUAL_QUOTE (ANY_NO_QUOTE|TOK_CANCELLED_DUAL_QUOTE|TOK_CANCELLED_BACKSLASH|string_single_quote_child)* TOK_DUAL_QUOTE
string_no_quote ::= (word|WHITE_SPACE)+
string ::= string_single_quote_parent|string_dual_quote_parent|string_no_quote

string_single_quote_child_with_selectors ::= TOK_SINGLE_QUOTE (entities|ANY_NO_QUOTE|TOK_CANCELLED_SINGLE_QUOTE|TOK_CANCELLED_DUAL_QUOTE|TOK_CANCELLED_BACKSLASH)* TOK_SINGLE_QUOTE
string_dual_quote_child_with_selectors ::= TOK_DUAL_QUOTE (entities|ANY_NO_QUOTE|TOK_CANCELLED_SINGLE_QUOTE|TOK_CANCELLED_DUAL_QUOTE|TOK_CANCELLED_BACKSLASH)* TOK_DUAL_QUOTE
string_single_quote_parent_with_selectors ::= TOK_SINGLE_QUOTE (entities|ANY_NO_QUOTE|TOK_CANCELLED_SINGLE_QUOTE|TOK_CANCELLED_BACKSLASH|string_dual_quote_child_with_selectors)* TOK_SINGLE_QUOTE
string_dual_quote_parent_with_selectors ::= TOK_DUAL_QUOTE (entities|ANY_NO_QUOTE|TOK_CANCELLED_DUAL_QUOTE|TOK_CANCELLED_BACKSLASH|string_single_quote_child_with_selectors)* TOK_DUAL_QUOTE
string_no_quote_with_selectors ::= (word|entities|WHITE_SPACE)+
string_with_selectors ::= string_single_quote_parent_with_selectors|string_dual_quote_parent_with_selectors|string_no_quote_with_selectors

nbt_object ::= TOK_OPEN_CURLY word TOK_COLON nbt_value(TOK_COMMA word TOK_COLON nbt_value)* TOK_CLOSE_CURLY
nbt_array ::= TOK_OPEN_SQUARE (ARRAY_TYPE_MARKER TOK_SEMI_COLON)? (nbt_value(TOK_COMMA nbt_value)*)? TOK_CLOSE_SQUARE
nbt_value ::= nbt_object|nbt_array|BOOLEAN|integer|float|string

// ------------------------------------------------------------------
// entity selectors
// ------------------------------------------------------------------

// positional
position_argument ::= SELECTOR_ARGUMENT_XYZ TOK_EQUAL float
distance_argument ::= SELECTOR_ARGUMENT_DISTANCE TOK_EQUAL u_float_range
volume_argument ::= SELECTOR_ARGUMENT_DXYZ TOK_EQUAL fractional

// scoreboard value
scores_argument ::= SELECTOR_ARGUMENT_SCORES TOK_EQUAL TOK_OPEN_CURLY word TOK_EQUAL integer_range(TOK_COMMA word TOK_EQUAL integer_range)*TOK_CLOSE_CURLY
tag_argument ::= SELECTOR_ARGUMENT_TAG TOK_EQUAL TOK_NEGATE?word?
team_argument ::= SELECTOR_ARGUMENT_TEAM TOK_EQUAL TOK_NEGATE?word?

// traits
sort_argument ::= SELECTOR_ARGUMENT_SORT TOK_EQUAL SORT_KEYWORDS
level_argument ::= SELECTOR_ARGUMENT_LEVEL TOK_EQUAL integer_range
gamemode_argument ::= SELECTOR_ARGUMENT_GAMEMODE TOK_EQUAL TOK_NEGATE?GAMEMODE_KEYWORDS
name_argument ::= SELECTOR_ARGUMENT_NAME TOK_EQUAL TOK_NEGATE?word
rotation_argument ::= SELECTOR_ARGUMENT_XY_ROTATION TOK_EQUAL float_range
type_argument ::= SELECTOR_ARGUMENT_TYPE TOK_EQUAL TOK_NEGATE?resource_location_tag
nbt_argument ::= SELECTOR_ARGUMENT_NBT TOK_EQUAL nbt_object
advancements_argument ::= SELECTOR_ARGUMENT_ADVANCEMENTS TOK_EQUAL TOK_OPEN_CURLY resource_location TOK_EQUAL (BOOLEAN|(TOK_OPEN_CURLY word TOK_EQUAL BOOLEAN(TOK_COMMA word TOK_EQUAL BOOLEAN)* TOK_CLOSE_CURLY))TOK_CLOSE_CURLY
predicate_argument ::= SELECTOR_ARGUMENT_PREDICATE TOK_EQUAL TOK_NEGATE?resource_location

entity_selector_argument_no_limit ::=
    position_argument|distance_argument|volume_argument|
    scores_argument|tag_argument|team_argument|
    sort_argument|level_argument|gamemode_argument|name_argument|rotation_argument|type_argument|nbt_argument|advancements_argument|predicate_argument
entity_selector_argument ::= entity_selector_argument_no_limit|limit_argument

limit_argument ::= SELECTOR_ARGUMENT_LIMIT TOK_EQUAL u_integer {pin=1}
limit_1_argument ::= SELECTOR_ARGUMENT_LIMIT TOK_EQUAL INTEGER_ONE {pin=1}

limit_1_entity_selector_argument_block ::= TOK_OPEN_SQUARE (limit_1_argument|entity_selector_argument_no_limit)(TOK_COMMA (limit_1_argument|entity_selector_argument_no_limit))* TOK_CLOSE_SQUARE
entity_selector_argument_block ::= TOK_OPEN_SQUARE (entity_selector_argument)(TOK_COMMA entity_selector_argument)* TOK_CLOSE_SQUARE

base_unique_entity_selector ::= TOK_AT_P|TOK_AT_R
base_multi_entity_selector ::= TOK_AT_A|TOK_AT_E
forced_unique_entity_selector ::= TOK_AT_S

unique_entity_selector ::=
    (base_unique_entity_selector limit_1_entity_selector_argument_block?)|
    (base_multi_entity_selector limit_1_entity_selector_argument_block)|
    (forced_unique_entity_selector limit_1_entity_selector_argument_block?)
multi_entity_selector ::=
    (base_unique_entity_selector entity_selector_argument_block?)|
    (base_multi_entity_selector entity_selector_argument_block?)|
    (forced_unique_entity_selector entity_selector_argument_block?)

entities ::= multi_entity_selector|PLAYER_PSEUDO|UUID
entity ::= unique_entity_selector|PLAYER_PSEUDO|UUID

// ------------------------------------------------------------------
// commands
// ------------------------------------------------------------------

command ::= (killCommand|tpCommand|banCommand|effectCommand|functionCommand|gamemodeCommand|itemCommand) {recoverWhile="command_recover"}
private command_recover ::= !CRLF

// advancement
/*
advancement (grant|revoke) <targets> everything
advancement (grant|revoke) <targets> only <advancement> [<criterion>]
advancement (grant|revoke) <targets> from <advancement>
advancement (grant|revoke) <targets> through <advancement>
advancement (grant|revoke) <targets> until <advancement>
<targets> entities
<advancement> resource_location
<criterion> string
 */
// advancementCommand := ADVANCEMENT TOK_GRANT_OR_REVOKE entities (TOK_EVERYTHING|(TOK_ONLY RESOURCE_LOCATION CRITERION?)|(TOK_ADVANCEMENT_LIMITS RESOURCE_LOCATION))

// attribute
/*
attribute <target> <attribute> get [<scale>]
attribute <target> <attribute> base get [<scale>]
attribute <target> <attribute> base set <value>
attribute <target> <attribute> modifier add <uuid> <name> <value> (add|multiply|multiply_base)
attribute <target> <attribute> modifier remove <uuid>
attribute <target> <attribute> modifier value get <uuid> [<scale>]
<target> entity
<attribute> resource_location
<scale> double
<uuid> uuid 00000001-0002-0003-0004-000000000005
<name> string "[-+._A-Za-z0-9]+"
<value> double
 */

// ban
/*
ban <targets> [<reason>]
<targets> players
<reason> string_with_selectors
 */
banCommand ::= COMMAND_BAN entities string_with_selectors? {pin=1}

// ban-ip

// banlist

// bossbar

// clear

// clone

// data

// datapack

// debug

// defaultgamemode

// deop

// difficulty

// effect
/*
effect clear [<targets>] [<effect>]
effect give <targets> <effect> [<seconds>] [<amplifier>] [<hideParticles>]
<targets> entities
<effect> mob_effect
<seconds> u_integer
<amplifier> u_integer
<hideParticles> boolean
 */
effectCommand ::= COMMAND_EFFECT (KEY_CLEAR effectClearArguments)|(KEY_GIVE effectGiveArguments) {pin=1}
effectClearArguments ::= entities? mob_effect?
effectGiveArguments ::= entities mob_effect integer? integer? BOOLEAN?

// enchant

// execute

// experience

// fill

// forceload

// function
functionCommand ::= COMMAND_FUNCTION resource_location_tag {pin=1}

// gamemode
gamemodeCommand ::= COMMAND_GAMEMODE GAMEMODE_KEYWORDS entities? {pin=1}

// gamerule

// give

// help

// item
/*
item modify (block <pos>|entity <targets>) <slot> <modifier>
item replace (block <pos>|entity <targets>) <slot> with <item> [<count>]
item replace (block <pos>|entity <targets>) <slot> from (block <sourcePos>|entity <sourceTarget>) <sourceSlot> [<modifier>]
 */
itemCommand ::= COMMAND_ITEM itemReplaceArguments|itemModifyArguments {pin=1}
itemCommonArguments ::= (KEY_BLOCK vec3)|(KEY_ENTITY entities) item_slot
itemModifyArguments ::= KEY_MODIFY itemCommonArguments resource_location
itemReplaceArguments ::= KEY_REPLACE itemCommonArguments (KEY_WITH item_stack integer?)|(KEY_FROM itemCommonArguments) resource_location?

// jfr

// kick

// kill
killCommand ::= COMMAND_KILL entities {pin=1}

// list

// locate

// locatebiome

// loot

// me

// msg

// op

// pardon

// pardon-ip

// particle

// perf

// playsound

// publish

// recipe

// reload

// save-all

// save-off

// save-on

// say

// schedule

// scoreboard

// seed

// setblock

// setidletimeout

// setworldspawn

// spawnpoint

// spectate

// spreadplayers

// stop

// stopsound

// summon

// tag

// team

// teammsg

// teleport
tpCommand ::= COMMAND_TELEPORT ((entities entity)|(entities tpVecArgs)|entity|tpVecArgs) {pin=1}
tpVecArgs ::= vec3 (float|tpFacingArg)?
tpFacingArg ::= KEY_FACING (vec3|(KEY_ENTITY entity)) ENTITY_ANCHOR?

// tell

// tellraw

// time

// title

// tm

// tp

// trigger

// w

// weather

// whitelist

// worldborder

// xp